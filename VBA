Sub ExtractTabsFromMultipleWorkbooks()

    Dim folderPath As String, outputFolderPath As String
    Dim fileName As String
    Dim sourceWorkbook As Workbook, destinationWorkbook As Workbook
    Dim ws As Worksheet

    folderPath = "C:\Users\cyang\Downloads\test\" ' Source folder - change this to your folder
    outputFolderPath = folderPath & "NewWorkbooks\" ' Output folder - change this to your desired output folder

    If Dir(outputFolderPath, vbDirectory) = "" Then
        MkDir outputFolderPath
    End If

    Dim sheetsToCopy As Variant
    sheetsToCopy = Array("Sheet1", "Sheet3") ' Modify the sheet names as needed

    Application.ScreenUpdating = False
    Application.DisplayAlerts = False

    fileName = Dir(folderPath & "*.xls*")
    Do While fileName <> ""
        Set sourceWorkbook = Workbooks.Open(folderPath & fileName)
        Set destinationWorkbook = Workbooks.Add

        For Each ws In sourceWorkbook.Sheets
            If Not IsError(Application.Match(ws.Name, sheetsToCopy, 0)) Then
                ws.Copy After:=destinationWorkbook.Sheets(destinationWorkbook.Sheets.Count)
                ws.Tab.Color = xlNone
            End If
        Next ws

        On Error Resume Next
        destinationWorkbook.Sheets("Sheet1").Delete
        On Error GoTo 0

        Dim outputFileName As String
        outputFileName = outputFolderPath & CleanFileName(GetBaseName(fileName) & " - Financial Reports Oct 2023.xlsx")
        destinationWorkbook.SaveAs outputFileName
        destinationWorkbook.Close

        sourceWorkbook.Save
        sourceWorkbook.Close False

        fileName = Dir()
    Loop

    Application.DisplayAlerts = True
    Application.ScreenUpdating = True

End Sub

Function CleanFileName(ByVal fileName As String) As String
    Dim invalidChars As String
    invalidChars = "\/:*?""<>|"

    For i = 1 To Len(invalidChars)
        fileName = Replace(fileName, Mid(invalidChars, i, 1), "")
    Next i

    If Len(fileName) > 250 Then
        fileName = Left(fileName, 250)
    End If

    CleanFileName = fileName
End Function

Function GetBaseName(ByVal fileName As String) As String
    Dim splitPos As Integer
    splitPos = InStr(fileName, "-")
    If splitPos > 0 Then
        GetBaseName = Trim(Left(fileName, splitPos - 1))
    Else
        GetBaseName = Trim(fileName)
    End If
End Function


